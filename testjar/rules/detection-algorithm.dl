// å¯¹äº ååºåˆ—åŒ– æ¼æ´æ£€æµ‹æ¥è¯´ï¼Œ
// éœ€è¦è€ƒè™‘åˆ°å­ç±»çš„å‡½æ•°è°ƒç”¨ã€‚
// ä½†åŸ doop ä¸­çš„ æ±¡ç‚¹åˆ†æä¸­

// é¦–å…ˆï¼ŒContextRequest æ˜¯ç¼ºå°‘çš„
// å­˜åœ¨ ContextRequestï¼Œæ‰å¯ä»¥åˆ†æ generalization-call-graph

.decl fromSourceCallGraph(?fromMethod: Method, ?toMethod: Method)
.decl isMethodInSourceCallGraph(?method: Method)

// å¿…é¡»è¦é€’å½’äº†
// ä» source çš„ caller å¼€å§‹
fromSourceCallGraph("<Start Method>", ?fromMethod) :-
    Instruction_Method(?invocation, ?fromMethod),
    // sourceMethod
    TaintSourceMethod(_, ?sourceMethod),
    MethodInvocationInContext(_, ?invocation, ?sourceMethod),
    Stats_Simple_Application_ReachableMethod(?fromMethod).

// å¼€å§‹é€’å½’
fromSourceCallGraph(?fromMethod, ?toMethod) :-
    fromSourceCallGraph(_, ?fromMethod),
    mainAnalysis.AnyCallGraphEdge(?invocation, ?toMethod),
    Instruction_Method(?invocation, ?fromMethod).

isMethodInSourceCallGraph(?toMethod) :-
    fromSourceCallGraph(_, ?toMethod).

// subMethod take from souffle-logic/addons/server-logic/queries.dl
.decl Method_Subtype(?method:Method, ?subMethod:Method)

Method_Subtype(?method, ?subMethod) :-
  Method_DeclaringType(?method, ?type),
  Method_SimpleName(?method, ?name),
  Method_Descriptor(?method, ?desc),
  ?name != "<clinit>",
  ?name != "<init>",
  basic.SubtypeOfDifferent(?subType, ?type),
  // _Valid_Interesting_Type(?type),
  // _Valid_Interesting_Type(?subType),
  Method_DeclaringType(?subMethod, ?subType),
  Method_SimpleName(?subMethod, ?name),
  Method_Descriptor(?subMethod, ?desc).

mainAnalysis.configuration.ContextRequest(?callerCtx, ?hctx, ?invocation, ?value, ?subMethod, 1) :-
    mainAnalysis.OptVirtualMethodInvocationBase(?invocation, ?base),
    mainAnalysis.VarPointsTo(?hctx, ?value, ?callerCtx, ?base),
    mainAnalysis.Value_Type(?value, ?valuetype),
    // constructer éœ€è¦çš„æ˜¯ subMethod
    basic.ResolveInvocation(?valuetype, ?invocation, ?method),
    basic.ResolveInvocation(?subValuetype, ?subInvocation, ?subMethod),
    Method_Subtype(?method, ?subMethod),
    isMethodInSourceCallGraph(?method).

// æœ‰ç‚¹å°åƒæƒŠï¼Œä¸Šé¢çš„ä»£ç å±…ç„¶èƒ½è·‘ï¼Œæˆ‘è¿˜æœ‰ç‚¹ç–‘æƒ‘ğŸ¤”ï¸
// ç”šè‡³å¯ä»¥ç›´æ¥å½±å“åˆ° TaintæŒ‡é’ˆéƒ¨åˆ†

// è¡¥å…… LeakingTaintedInformation
// ä¸»è¦é’ˆå¯¹ä»¥ä¸‹ç‰¹æ®Šçš„æ½œåœ¨æ¼æ´
// 0 methodCallsink() æ— å‡½æ•°å‚æ•°ï¼Œä¸”æ±¡ç‚¹æºä¸º base(this)
// 1. sink æ— å‚æ•°
// 2. sink ä¸­æœ‰å‚æ•°ï¼Œä¸”ä¸ source å­˜åœ¨å…³ç³»
// 3. sink ä¸­æœ‰å‚æ•°ï¼Œéƒ½ä¸ source æ— å…³ç³»

// LeakingTaintedInformation(?sourceLabel, ?destLabel, ?ctx, ?invocation, ?source) :-
//   SourceFromTaintedValue(?value, ?source),
//   LabelFromSource(?source, ?sourceLabel),
//   TaintedVarPointsTo(?value, ?ctx, ?var),
//   LeakingSinkVariable(?destLabel, ?invocation, ?ctx, ?var).

// å®½æ³›å®šä¹‰
LeakingTaintedInformation(?sourceLabel, ?destLabel, ?ctx, ?invocation, ?source) :-
    // å¯¹äºä¸Šé¢çš„ç‰¹æ®Š Sink, åˆ¤æ–­å½“å‰å‡½æ•°çš„ base çš„ caller ä¸­çš„ this æ˜¯å¦è¢«æ±¡æŸ“
    SourceFromTaintedValue(?value, ?source),
    LabelFromSource(?source, ?sourceLabel),
    TaintedVarPointsTo(?value, ?ctx, ?var),
    // ?var ä¸º caller çš„ this
    ThisVar(?inMethod, ?var),
    Instruction_Method(?invocation, ?inMethod),
    MethodInvocation_Base(?invocation, ?varBase),
    // ?varBase ä¸º caller çš„ æŸä¸ª callee çš„ base/receiver
    LeakingSinkVariable(?destLabel, ?invocation, ?ctx, ?varBase).

.output fromSourceCallGraph

// æ·»åŠ  SerHybrid ä¸­çš„éƒ¨åˆ†é€»è¾‘
// heap_flowl.dl
#include "SerHybrid/heap_flow.dl"

// ä¿®æ”¹ SerHybrid ä¸­çš„ taint.dl

.decl ReachableSinks(?invocation:MethodInvocation)
.decl ReachableTaintedSinks(?invocation:MethodInvocation, ?param:Var)
.decl SinkVariable(?param:Var)

ReachableSinks(?invocation) :-
    LeakingTaintedInformation(_, _, _, ?invocation, _).

ReachableTaintedSinks(?invocation, ?actual) :-
    ReachableSinks(?invocation),
    ActualParam(_, ?invocation, ?actual), // FIXME set index in sink
    mainAnalysis.VarPointsTo(_, ?value, _, ?actual),
    MockObject(?value, _).

ReachableTaintedSinks(?invocation, ?actual) :-
    ReachableSinks(?invocation),
    // ?actual ä¸º base
    ?actual = ?varBase,
    MethodInvocation_Base(?invocation, ?varBase),
    mainAnalysis.VarPointsTo(_, _, _, ?varBase).

SinkVariable(?v) :-
   ReachableTaintedSinks(_, ?v).

.output SinkVariable
.output ReachableSinks
.output ReachableTaintedSinks
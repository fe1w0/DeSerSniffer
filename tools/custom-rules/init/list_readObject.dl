#define LimitReadObject 1
#define ListReadObject 1
// #define ListReadObjectOnlyApplication 1
#define ListReadObjectOnlyData 1

#include "../basic/define-source-and-sink-method-name.dl"
#include "../basic/data-structure.dl"

/**
 * list_readObject 需要列出所有可利用readObject来实现的类。
 * 1. 需要类继承 java.io.Serializable
 * 2. 函数名为 void readObject(java.io.ObjectInputStream)
 * list_readObject Module:
 * DirectSerializableClass
 * InheritSerializableClass
 * ListReadObjectClass
*/

/**
 * ReadObject 函数
*/
.decl ReadObjectMethod(method: Method)

ReadObjectMethod(method) :-
    Method_SimpleName(method, "readObject"),
    Method_ParamTypes(method, "java.io.ObjectInputStream"),
    Method_ReturnType(method, "void").

/**
 * DirectSerializableClass:
 * 查找那些直接实现 void readObject(java.io.ObjectInputStream)
*/
.decl DirectSerializableClass(class: ClassType)
.output DirectSerializableClass

DirectSerializableClass(class) :-
    basic.SupertypeOf("java.io.Serializable", class),
    Method_DeclaringType(method, class),
    ReadObjectMethod(method).

/**
 * InheritSerializableClass:
 * 查找那些间接实现 void readObject(java.io.ObjectInputStream)，需要排除 DirectSerializableClass
*/
.decl InheritSerializableClass(class: ClassType)
InheritSerializableClass(class) :-
    DirectSerializableClass(sup_class),
    !DirectSerializableClass(class),
    basic.SupertypeOf(sup_class, class).

/**
 * label 用于区分 DirectSerializableClass 和 InheritSerializableClass
*/
.decl ListReadObjectClass(class: ClassType, label: symbol)
.printsize ListReadObjectClass
.output ListReadObjectClass


#ifdef LimitReadObject
#ifndef ListReadObjectOnlyData
ListReadObjectClass(class, label) :-
    ApplicationClass(class),
    DirectSerializableClass(class),
    !DenyEntryClass(class),
    !AbortClassJavaLang(class),
    !DataStructureInnerClass(class),
    !ApplicationClassInnerClass(class),
    label = "DirectSerializableApplicationClass".
#endif

#ifndef ListReadObjectOnlyApplication
ListReadObjectClass(class, label) :-
    DataStructureClass(class),
    !ApplicationClass(class),
    DirectSerializableClass(class),
    !DenyEntryClass(class),
    !AbortClassJavaLang(class),
    !DataStructureInnerClass(class),
    !ApplicationClassInnerClass(class),
    label = "DirectSerializableDataStructureClass".
#endif
#endif

#ifndef LimitReadObject
ListReadObjectClass(class, label) :-
    DirectSerializableClass(class),
    (
        label = "DirectSerializableDataStructureClass"
        ; label = "DirectSerializableDataStructureClass"
    ).

ListReadObjectClass(class, label) :-
    InheritSerializableClass(class),
    label = "InheritSerializableClass".
#endif